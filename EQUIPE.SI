(*******************************************************************)
(*  Module       : Equipement                                      *)
(*  Description  : D‚termine l'‚quipement install‚.                *)
(*******************************************************************)
(*  Auteur       : Sylvain Maltais                                 *)
(*  Date cr‚e    : 1991/12/31                                      *)
(*  Date modifi‚ : 1992/02/06                                      *)
(*******************************************************************)

(*******************************************************************)
(*                             Constante                           *)
(*******************************************************************)

Const  { Constante de la carte vid‚o }
      MDA         =  1;
      HGC         =  2;
      CGA         =  3;
      GS          =  4;
      EGA         =  5;
      PGA         =  6;
      MCGA        =  7;
      VGA         =  8;
      IBM8514     =  9;
      ITVGA2      = 10;
      ATIVGA      = 11;

       { Constante du modŠle d'ordinateur }
      PC          = 255; { FFh }
      XT          = 254; { FEh }
      PCJunior    = 253; { FDh }
      AT          = 252; { FCh }
      PS2Model30  = 250; { FAh }
      Convertible = 249; { F9h }
      PS2Model80  = 248; { F8h }
      ContermMax  = 086; { 56h }

(*******************************************************************)
(*                              Type                               *)
(*******************************************************************)

Type StrVer    = String[7];
     StrDate   = String[15];
     VideoData = Record
		  Moniteur : Array[0..1] of Byte;
		  Carte    : Array[0..1] of Byte;
		  Memoire  : Array[0..1] of LongInt;
		  ROMBIOS  : Array[0..1] of Boolean;
		  Version  : Array[0..1] of StrVer;
		  Date     : Array[0..1] of StrDate;
		 End;

(*******************************************************************)
(*                            Variable                             *)
(*******************************************************************)

Var Model : Byte Absolute $FFFF:$E;

(*******************************************************************)
(*  Fonction    : Microprocesseur                                  *)
(*  Description : D‚termine le microprocesseur install‚.           *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : 0 si Intel 8088,                                 *)
(*                1 si Intel 8086,                                 *)
(*                2 si Nec V20,                                    *)
(*                3 si Nec V30,                                    *)
(*                4 si Intel 80188,                                *)
(*                5 si Intel 80186,                                *)
(*                6 si Intel 80286 ou                              *)
(*                7 si Intel 80386.                                *)
(*******************************************************************)

Function Microprocesseur:Byte;
Begin
 InLine($9C/         {           PushF          }
        $51/         {           Push CX        }
        $52/         {           Push DX        }
        $57/         {           Push DI        }
        $56/         {           Push SI        }
        $06/         {           Push ES        }
        $33/$C0/     {           Xor  AX,AX     }
        $50/         {           Push AX        }
        $9D/         {           PopF           }
        $9C/         {           PushF          }
        $58/         {           Pop  AX        }
        $25/$00/$F0/ {           And  AX,F000h  }
        $3D/$00/$F0/ {           Cmp  AX,F000h  }
        $74/$13/     {           Je   Pas_386   }
        $B2/$06/     {           Mov  DL,06h    }
        $B8/$00/$70/ {           Mov  AX,7000h  }
        $50/         {           Push AX        }
        $9D/         {           PopF           }
        $9C/         {           PushF          }
	$58/         {           Pop  AX        }
        $25/$00/$70/ {           And  AX,7000h  }
        $74/$36/     {           Je   PFin      }
        $FE/$C2/     {           Inc  DL        }
        $EB/$32/$90/ {           Jmp  PFin      }
        $B2/$04/     { Pas_386 : Mov  DL,04h    }
        $B0/$FF/     {           Mov  AL,FFh    }
        $B1/$21/     {           Mov  CL,21h    }
        $D2/$E8/     {           Shr  AL,CL     }
        $75/$12/     {           Jne  T88_86    }
        $B2/$02/     {           Mov  DL,02h    }
        $FB/         {           STI            }
        $BE/$00/$00/ {           Mov  SI,0000h  }
        $B9/$FF/$FF/ {           Mov  CX,FFFFh  }
        $F3/$26/$AC/ {           Rep  Lods Byte Ptr ES:[SI] }
        $0B/$C9/     {           Or   CX,CX     }
        $74/$02/     {           Je   T88_86    }
        $B2/$00/     {           Mov  DL,00h    }
        $0E/         { T88_86  : Push CS        }
        $07/         {           Pop  ES        }
        $FD/         {           Std            }
	$B0/$FB/     {           Mov  AL,FBh    }
        $B9/$03/$00/ {           Mov  CX,0003h  }
        $E8/$16/$00/ {           Call Get_DI    }
        $FA/         { T86_1   : Cli            }
        $F3/$AA/     {           Rep  Stosb     }
        $FC/         {           Cld            }
        $90/         {           Nop            }
        $90/         {           Nop            }
	$90/         {           Nop            }
        $42/         {           Inc  DX        }
        $90/         {           Nop            }
        $FB/         { Q_end   : Sti            }
        $88/$56/$FF/ { PFin    : Mov  [BP-1],DL }
        $07/         {           Pop  ES        }
        $5E/         {           Pop  SI        }
        $5F/         {           Pop  DI        }
        $5A/         {           Pop  DX        }
        $59/         {           Pop  CX        }
        $9D/         {           PopF           }
        $EB/$07/$90/ {           Jmp  Fin       }
        $5F/         {           Pop  DI        }
        $83/$C7/$09/ {           Add  DI,0009h  }
        $EB/$E4);    {           Jmp  T86_1     }
End;

(*******************************************************************)
(*  Fonction    : Coprocesseur                                     *)
(*  Description : D‚termine le coprocesseur install‚.              *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : 0 si Intel 8087,                                 *)
(*                1 si Intel 80187,                                *)
(*                2 si Intel 80287 ou                              *)
(*                3 si Intel 80387.                                *)
(*******************************************************************)

Function Coprocesseur:Byte;
Begin
 If(Test8087 > 0)Then
 Begin
  If(Microprocesseur in [4,5])Then Coprocesseur := 2
   else
  Begin
   If(Test8087 = 1)Then Coprocesseur := Test8087
		   Else Coprocesseur := Test8087 + 1;
  End;
 End
  else
 Coprocesseur := 0;
End;

(*******************************************************************)
(*  Fonction    : Nom Coprocesseur                                 *)
(*  Description : D‚termine le nom du coprocesseur install‚.       *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : NomCoprocesseur                                  *)
(*******************************************************************)

Function NomCoprocesseur:String;
Var Tempory : String;
Begin
 If(Test8087 = 0)Then NomCoprocesseur := 'Absent'
  else
 Begin
  If(Test8087 > 1)Then Str(Coprocesseur-1,Tempory)
		  Else Tempory := '';
  NomCoprocesseur := 'Intel 80'+Tempory+'87';
 End;
End;

(*******************************************************************)
(*  Fonction    : Microprocesseur                                  *)
(*  Description : D‚termine le nom du microprocesseur install‚.    *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : NomMicroprocesseur                               *)
(*******************************************************************)

Function NomMicroprocesseur:String;
Var Tempory : String;
Begin
 Str(Microprocesseur-4,Tempory);
 Case Microprocesseur of
  0 : NomMicroprocesseur := 'Intel 8088';
  1 : NomMicroprocesseur := 'Intel 8086';
  2 : NomMicroprocesseur := 'Nec V20';
  3 : NomMicroprocesseur := 'Nec V30';
  4 : NomMicroprocesseur := 'Intel 80188';
   else
  NomMicroprocesseur := 'Intel 80'+Tempory+'86';
 End;
End;

 { Registre de travail }

(*******************************************************************)
(*  Fonction    : Registre Accumulateur                            *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreAccumulateur:String;
Begin
 RegistreAccumulateur := 'AX (AH,AL)';
End;

(*******************************************************************)
(*  Fonction    : Registre Base                                    *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreBase:String;
Begin
 RegistreBase := 'BX (BH,BL)';
End;

(*******************************************************************)
(*  Fonction    : Registre Compteur                                *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreCompteur:String;
Begin
 RegistreCompteur := 'CX (CH,CL)';
End;

(*******************************************************************)
(*  Fonction    : Registre Donn‚e                                  *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreDonne:String;
Begin
 RegistreDonne := 'DX (DH,DL)';
End;

 { Registre OffSet }

(*******************************************************************)
(*  Fonction    : Registre Source Index                            *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreSourceIndex:String;
Begin
 RegistreSourceIndex := 'SI';
End;

(*******************************************************************)
(*  Fonction    : Registre Destination Index                       *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreDestinationIndex:String;
Begin
 RegistreDestinationIndex := 'DI';
End;

(*******************************************************************)
(*  Fonction    : Registre Instruction Pointer                     *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreInstructionPointer:String;
Begin
 RegistreInstructionPointer := 'IP';
End;

(*******************************************************************)
(*  Fonction    : Registre Base Pointer                            *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreBasePointer:String;
Begin
 RegistreBasePointer := 'BP';
End;

(*******************************************************************)
(*  Fonction    : Registre Stack Pointer                           *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreStackPointer:String;
Begin
 RegistreStackPointer := 'SP';
End;

 { Registre Segment }

(*******************************************************************)
(*  Fonction    : Registre Code Segment                            *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreCodeSegment:String;
Begin
 RegistreCodeSegment := 'CS';
End;

(*******************************************************************)
(*  Fonction    : Registre Data Segment                            *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreDataSegment:String;
Begin
 RegistreDataSegment := 'DS';
End;

(*******************************************************************)
(*  Fonction    : Registre Extra Segment                           *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreExtraSegment:String;
Begin
 RegistreExtraSegment := 'ES';
End;

(*******************************************************************)
(*  Fonction    : Registre Stack Segment                           *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreStackSegment:String;
Begin
 RegistreStackSegment := 'SS';
End;

 { Registre Option }

(*******************************************************************)
(*  Fonction    : Registre Flags                                   *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function RegistreFlags:String;
Begin
 RegistreFlags := 'CF,PF,AF,ZF,SF,OF,TF,IF,DF';
End;

(*******************************************************************)
(*  Proc‚dure   : Configuration Vid‚o                              *)
(*  Description : Retourne le mat‚riel vid‚o disponible sur cet    *)
(*                ordinateur.                                      *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : Data :                                           *)
(*                 Moniteur,                                       *)
(*                 Carte,                                          *)
(*                 Memoire,                                        *)
(*                 ROMBIOS (BIOS en ROM),                          *)
(*                 Version (Version de la carte) et                *)
(*                 Date (Data de la carte).                        *)
(*******************************************************************)

Procedure ConfigurationVideo(Var Data:VideoData);
Var Registre        : Registers;
    Temp            : Array[0..63] of Byte;
    Pos             : Byte;
    TempPtr         : Pointer;
{ 00 = Aucun     00 = Aucun
  01 = MDA       01 = Monochrome
  02 = HGC       02 = Couleur
  03 = CGA       03 = Couleur Haute-Resolution
  04 = GS        04 = 5175
  05 = EGA       05 = Monochrome digital
  06 = PGA       06 = Couleur digital
  07 = MCGA      07 = Monochrome analogique
  08 = VGA       08 = Couleur analogique
  09 = IBM8514/A 09 = Analogique
  10 = IT-VGA2   10 = Nulle
  11 = ATI VGA   11 = Nulle
  FF = Inconnu   FF = Inconnu }

 Function Teste6845(PortVideo:Word):Byte;
 Var Temp1 : Byte;
     Temp2 : Byte;
 { Mono    = 3B4h
   Couleur = 3D4h }
 Begin
  Port[PortVideo] := 12;
  Inc(PortVideo);
  Temp1 := Port[PortVideo];
  Port[PortVideo] := $10;
  InLine($B9/$00/$01/ {        Mov CX,0100h }
 	 $E2/$FE);    { Wait : Loop Wait    }
  Temp2 := Port[PortVideo];
  Port[PortVideo] := Temp1;
  If(Temp2 = $10)Then Teste6845 := 1
		 Else Teste6845 := 0;
 End;

 Function TesteHGC:Byte;
 Var AH : Byte;
     AL : Byte;
     CX : Word;
 Begin
  AH := Port[$3BA];
  AH := AH and $80;
  For CX := $8000 DownTo 0 do
  Begin
   AL := Port[$3BA];
   AL := AL and $80;
   If(AL <> AH)Then
   Begin
    TesteHGC := 1;
    Exit;
   End;
  End;
  TesteHGC := 0;
 End;

 Function TesteITVGA2:Byte;
 Var Chaine   : Array[0..8] of Byte Absolute $C000:$0181;
     Temp     : String[15];
     Compteur : Byte;
 Begin
  Temp := '';
  For Compteur := 0 to 8 do
  Begin
   Temp := Temp + Chr(Chaine[Compteur]);
  End;
  If(Temp = 'IT - VGA2')Then TesteITVGA2 := 1
 		        Else TesteITVGA2 := 0;
 End;

 Function TesteATIVGA:Byte;
 Var Chaine   : Array[0..2] of Byte Absolute $C000:$00F9;
     Temp     : String[15];
     Compteur : Byte;
 Begin
  Temp := '';
  For Compteur := 0 to 2 do
  Begin
   Temp := Temp + Chr(Chaine[Compteur]);
  End;
  If(Temp = 'ATI')Then TesteATIVGA := 1
 	          Else TesteATIVGA := 0;
 End;

 Function DateBIOSVideo:StrDate;
 Var Chaine    : Array[0..7] of Byte Absolute $C000:$8F;
     Temp      : String[15];
     Compteur  : Byte;
 Begin
  Temp := '';
  If(TesteITVGA2 = 1)Then
  Begin
   For Compteur := 0 to 7 do
   Begin
    Temp := Temp + Chr(Chaine[Compteur]);
   End;
  End
   else
  Temp := 'Inconnue';
  DateBIOSVideo := Temp;
 End;

 Function VersionBIOSVideo:StrVer;
 Var Chaine    : Array[0..3] of Byte Absolute $C000:$0099;
     ChaineATI : Array[0..5] of Byte Absolute $C000:$011F;
     Temp      : String[7];
     Compteur  : Byte;
 Begin
  Temp := '';
  If(TesteATIVGA = 1)Then
  Begin
   For Compteur := 0 to 5 do
   Begin
    Temp := Temp + Chr(ChaineATI[Compteur]);
   End;
  End
   else
  If(TesteITVGA2 = 1)Then
  Begin
   For Compteur := 0 to 3 do
   Begin
    Temp := Temp + Chr(Chaine[Compteur]);
   End;
  End
   else
  Temp := 'Inconnu';
  VersionBIOSVideo := Temp;
 End;

 Procedure NumeroCaseVGAPS2(Numero:Byte);
 Begin
  Case Temp[Numero] of
 $00 : Begin
	Data.Carte[Numero]    := 0; { Aucun }
	Data.Moniteur[Numero] := 0; { Aucun }
       End;
 $01 : Begin
	Data.Carte[Numero]    := MDA; { MDA }
	Data.Moniteur[Numero] := 1; { Monochrome }
       End;
 $02 : Begin
	Data.Carte[Numero]    := CGA; { CGA }
	Data.Moniteur[Numero] := 2; { Couleur }
       End;
 $04 : Begin
	Data.Carte[Numero]    := EGA; { EGA }
	Data.Moniteur[Numero] := 3; { Couleur Haute-Resolution }
       End;
 $05 : Begin
	Data.Carte[Numero]    := EGA; { EGA }
	Data.Moniteur[Numero] := 1; { Monochrome }
       End;
 $06 : Begin
	Data.Carte[Numero]    := PGA; { PGA }
	Data.Moniteur[Numero] := 4; { Moniteur 5175 }
       End;
 $07 : Begin
	Data.Carte[Numero]    := VGA; { VGA }
	Data.Moniteur[Numero] := 7; { Monochrome Analogique }
       End;
 $08 : Begin
	Data.Carte[Numero]    := VGA; { VGA }
	Data.Moniteur[Numero] := 8; { Couleur Analogique }
       End;
 $0A : Begin
	Data.Carte[Numero]    := MCGA; { MCGA }
	Data.Moniteur[Numero] := 6; { Couleur digital }
       End;
 $0B : Begin
	Data.Carte[Numero]    := MCGA; { MCGA }
	Data.Moniteur[Numero] := 5; { Monochrome digital }
       End;
 $0C : Begin
	Data.Carte[Numero]    := MCGA; { MCGA }
	Data.Moniteur[Numero] := 8; { Couleur Analogique }
       End;
 $FF : Begin
	Data.Carte[Numero]    := $FF; { Inconnue }
	Data.Moniteur[Numero] := $FF; { Inconnue }
       End;
  End;
 End;

Begin
 With Registre do With Data do
 Begin
  ROMBIOS[0] := False;
  ROMBIOS[1] := False;
  AH := $1A;
  AL := $00;
  Intr($10,Registre);
  If(AL = $1A)Then { Fonction support‚ ? }
  Begin
   Temp[0] := BL;
   Temp[1] := BH;
    { Combien de M‚moire ? }
   For Pos := 1 DownTo 0 do
   Begin
    NumeroCaseVGAPS2(Pos);
    Memoire[Pos] := 0;
    Case Carte[Pos] of
     VGA : Begin { VGA ? }
	    ROMBIOS[Pos] := True;
	    Memoire[Pos] := 262144;
            If(TesteATIVGA = 1)Then Carte[Pos] := ATIVGA else
	    If(TesteITVGA2 = 1)Then Carte[Pos] := ITVGA2 else
	    Begin
	     GetIntVec($7F,TempPtr);
	     If(Mem[Seg(TempPtr):Ofs(TempPtr)] <> $CF)Then
	     Begin
	      GetIntVec($F7,TempPtr);
	      If(Mem[Seg(TempPtr):Ofs(TempPtr)] <> $CF)Then
	       { Si Int F7h <> CFh alors IBM8514/A }
	      Begin
	       Carte[Pos]   := 9;
	       Memoire[Pos] := 524288;
	      End;
	     End;
	    End;
	   End;
     EGA : Begin { EGA ? }
	    ROMBIOS[Pos] := True;
	    AH := $12;
	    BL := $10;
	    Intr($10,Registre);
	    Memoire[Pos] := (BL+1)*65536;
	   End;
     CGA : Memoire[Pos]  := 16384; { CGA ? }
     MDA : Begin { MDA ? }
	    If(TesteHGC = 1)Then { HGC ? }
	    Begin
	     Carte[Pos] := HGC;
	     If(Port[$3BF] and 2=2)Then Memoire[Pos] := 65536
				   Else Memoire[Pos] := 32768;
	    End
	     else
	    Memoire[Pos]  := 4096; { 4Ko }
	   End;
    End;
   End;
  End
   else
  Begin
    { Teste EGA }
   ROMBIOS[Pos] := True;
   AH := $12;
   BL := $10;
   Intr($10,Registre);
   If(BL <> $10)Then { EGA ? }
   Begin
    If(MemW[$0040:$63] = $3D4)Then Pos := 0
			      Else Pos := 1;
    Carte[Pos] := EGA; { EGA }
    If(CL = $0B)Then Moniteur[Pos] := $01 else { Monochrome }
    If(CL = $08)Then Moniteur[Pos] := $02      { Couleur }
		Else Moniteur[Pos] := $03; { Multi Sync }
    Memoire[Pos] := (BL+1)*65536;
   End;
   If(Teste6845($3D4) = 1)Then { CGA ? }
   Begin
    If(MemW[$0040:$63] = $3D4)Then Pos := 0
			      Else Pos := 1;
    Carte[Pos]    := CGA;   { CGA }
    Moniteur[Pos] := $02;   { Couleur }
    Memoire[Pos]  := 16384; { 16Ko }
   End;
   If(Teste6845($3B4) = 1)Then { MDA ? }
   Begin
    If(MemW[$0040:$63] = $3B4)Then Pos := 0
			      Else Pos := 1;
    Moniteur[Pos] := $01; { Monochrome }
    If(TesteHGC = 1)Then { HGC ? }
    Begin
     Carte[Pos] := HGC;
     If(Port[$3BF] and 2=2)Then Memoire[Pos] := 65536
			   Else Memoire[Pos] := 32768;
    End
     else
    Begin
     Carte[Pos]    := MDA;  { MDA }
     Moniteur[Pos] := $01;  { Monochrome }
     Memoire[Pos]  := 4096; { 4Ko }
    End;
   End;
  End;
  For Pos := 0 to 1 do
  Begin
   If(Carte[Pos] > 5)Then
   Begin
    Date[Pos] := DateBIOSVideo;
    Version[Pos] := VersionBIOSVideo;
   End
    else
   Begin
    Date[Pos]    := 'Absent';
    Version[Pos] := 'Absent';
   End;
  End;
 End;
End;

(*******************************************************************)
(*  Fonction    : DriverMouse                                      *)
(*  Description : D‚termine la pr‚sence du Driver Mouse            *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : DriverMouse retourne 1 pour pr‚sent et 0 absent. *)
(*******************************************************************)

Function DriverMouse:Byte;
Var TempPtr : Pointer;
Begin
 GetIntVec($F7,TempPtr);
 If(Mem[Seg(TempPtr):Ofs(TempPtr)] <> $CF)Then DriverMouse := 1
					  Else DriverMouse := 0;
End;

(*******************************************************************)
(*  Fonction    : Teste XMS                                        *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : TesteXMS retourne 1 pour pr‚sent et 0 absent.    *)
(*******************************************************************)

Function TesteXMS:Byte;
Var Registre : Registers;
Begin
 With Registre do
 Begin
  AX := $4300;
  Intr($2F,Registre);
  If(AL = $80)Then TesteXMS := 1
	      Else TesteXMS := 0;
 End;
End;

(*******************************************************************)
(*  Fonction    : Teste CD-ROM                                     *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function TesteCDROM:Byte;
Var Registre : Registers;
Begin
 With Registre do
 Begin
  AX := $1500;
  BX := $0000;
  Intr($2F,Registre);
  TesteCDROM := BX;
 End;
End;

(*******************************************************************)
(*  Fonction    : SoftWare Propr‚taire Interruption                *)
(*******************************************************************)
(*  Entr‚e      : Numero                                           *)
(*******************************************************************)

Function SoftWareProprietaireInterruption(Numero:Byte):Byte;
Var TempPtr : Pointer;
{ Retourne :

   0 = Nulle,
   1 = BIOS,
   2 = DOS,
   3 = IRQ,
   4 = Fichier }
Begin
 GetIntVec(Numero,TempPtr);
 If(Numero in [$08..$0F])or
   ((Mem[$FFFF:$0E]=252)and(Numero in [$70..$77]))Then { IRQ ? }
 Begin
  SoftWareProprietaireInterruption := 3;
 End
  else
 If(Seg(TempPtr) = 0)and(Ofs(TempPtr) = 0)Then { Nulle ? }
 Begin
  SoftWareProprietaireInterruption := 0;
 End
  else
 If(Seg(TempPtr) > $BFFF)Then { BIOS ? }
 Begin
  SoftWareProprietaireInterruption := 1;
 End
  else
 Begin
  If(Mem[Seg(TempPtr):Ofs(TempPtr)] <> $CF)and
    (Numero in [$33,$60..$66,$7F,$F7])Then
  Begin
   SoftWareProprietaireInterruption := 4;
  End
   else
  Begin
   SoftWareProprietaireInterruption := 2;
  End;
 End;
End;

(*******************************************************************)
(*  Fonction    : Teste Super Bios II Formatter                    *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : TesteSB2F retourne 1 pour pr‚sent et 0 absent.   *)
(*******************************************************************)

Function TesteSB2F:Byte;
Var Chaine   : Array[0..22] of Byte Absolute $C800:$14F9;
    Temp     : String[31];
    Compteur : Byte;
Begin
 Temp := '';
 For Compteur := 0 to 22 do
 Begin
  Temp := Temp + Chr(Chaine[Compteur]);
 End;
 If(Temp = 'Super Bios II Formatter')Then TesteSB2F := 1
				     Else TesteSB2F := 0;
End;

Function DateSB2F:String;
Var Chaine   : Array[0..22] of Byte Absolute $C800:$153E;
    Temp     : String[31];
    Compteur : Byte;
Begin
 If(TesteSB2F = 1)Then
 Begin
  Temp := '';
  For Compteur := 0 to 3 do Temp := Temp + Chr(Chaine[Compteur]);
 End
  else
 Temp := '';
 DateSB2F := Temp;
End;

Function VerSB2F:String;
Var Chaine   : Array[0..22] of Byte Absolute $C800:$1516;
    Temp     : String[31];
    Compteur : Byte;
Begin
 If(TesteSB2F = 1)Then
 Begin
  Temp := '';
  For Compteur := 0 to 2 do Temp := Temp + Chr(Chaine[Compteur]);
 End
  else
 Temp := '';
 VerSB2F := Temp;
End;

Function ModeleSB2F:String;
Var Chaine   : Array[0..22] of Byte Absolute $C800:$1528;
    Temp     : String[31];
    Compteur : Byte;
Begin
 If(TesteSB2F = 1)Then
 Begin
  Temp := '';
  For Compteur := 0 to 15 do Temp := Temp + Chr(Chaine[Compteur]);
 End
  else
 Temp := '';
 ModeleSB2F := Temp;
End;

(*******************************************************************)
(*  Fonction    : Nom Machine                                      *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*******************************************************************)

Function NomMachine:String;
Var Chaine : String[15];

 Function RtnChn(Seg,Ofs:Word;Nombre:Byte):String;
 Var Temp     : String[15];
     Compteur : Byte;
 Begin
  Temp := '';
  For Compteur := 0 to Nombre do
  Begin
   Temp := Temp + Chr(Mem[Seg:Ofs+Compteur]);
  End;
  RtnChn := Temp;
 End;

Begin
 If(RtnChn($F000,$4,12)='AAAAMMMMIIII')Then Chaine := 'American Megatrend' else
 If(RtnChn($FFFE,$A,5)='COMPAQ')Then Chaine := 'COMPAG' else
 If(RtnChn($FC05,0,7)='OLIVETTI')Then Chaine := 'OLIVETTI' else
 If(RtnChn($FE00,4,5)='Tandon')Then Chaine := 'Tandon' else
 If(RtnChn($FC02,$B,4)='Tandy')Then Chaine := 'Tandy' else
 If(RtnChn($FFFC,2,3)='WANG')Then Chaine := 'WANG' else
 If(RtnChn($FE00,3,3)='WYSE')Then Chaine := 'WYSE' else
 If(RtnChn($FB00,0,5)='Zenith')Then Chaine := 'Zenith' else
 If(RtnChn($FC2F,$F,5)='Zenith')Then Chaine := 'Zenith' else
 If(RtnChn($FE00,$E,2)='IBM')Then Chaine := 'IBM'
  else
 Chaine := 'Machine "clone"';
 NomMachine := Chaine;
End;

(*******************************************************************)
(*  Fonction    : Joystick                                         *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : Joystick retourne 1 pour pr‚sent et 0 absent.    *)
(*******************************************************************)

Function Joystick:Byte;
Var Registre : Registers;
Begin
 Joystick := 0;
 If(Mem[$FFFF:$E] in [$FF,$FE,$FD])Then { PC,XT,PC Junior ? }
 Begin
  If(MemW[$0040:$10] and $4000 = $4000)Then Joystick := 1;
 End
  else
 If(Mem[$FFFF:$E] = $FC)Then { AT ? }
 Begin
  With Registre do
  Begin
   AH := $84;
   DX := $00;
   Intr($15,Registre);
   If(AL <> 0)Then Joystick := 1;
  End;
 End
  else
 Begin { La derniŠre possiblit‚ est d'interroger
	 le port joystick lui mˆme }
  If(Port[$201] <> 0)Then Joystick := 1;
 End;
End;

(*******************************************************************)
(*  Fonction    : Crayon Lumineux                                  *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : Crayon lumineux retourne 1 pour pr‚sent et 0     *)
(*                absent.                                          *)
(*******************************************************************)

Function CrayonLumineux:Byte;
Var Registre : Registers;
Begin
 With Registre do
 Begin
  AH := $04;
  Intr($10,Registre);
  CrayonLumineux := AH;
 End;
End;

(*******************************************************************)
(*  Fonction    : Date BIOS                                        *)
(*  Description : D‚termine la date du BIOS de l'ordinateur        *)
(*******************************************************************)
(*  Entr‚e      : Aucun                                            *)
(*  Sortie      : DateBIOS                                         *)
(*******************************************************************)

Function DateBIOS:String;
Var Chaine   : Array[0..7] of Byte Absolute $FFFF:$5;
    Temp     : String[15];
    Compteur : Byte;
Begin
 Temp := '';
 For Compteur := 0 to 7 do
 Begin
  Temp := Temp + Chr(Chaine[Compteur]);
 End;
 DateBIOS := Temp;
End;